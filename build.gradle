plugins {
	id 'org.springframework.boot' version '2.4.5'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'java'
	id 'war'
	id 'java-library' // QueryDSL must have (Java Library API implementation)
} 

group = 'com.recetario'
version = 'recetario-0.1'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	api 'com.querydsl:querydsl-jpa:4.4.0' // QueryDSL must have (Java Library API)

	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-hateoas'
	implementation 'org.springframework.boot:spring-boot-starter-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.projectlombok:lombok'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	
	annotationProcessor 'org.projectlombok:lombok'
	annotationProcessor 'com.querydsl:querydsl-apt:4.4.0:jpa' // QueryDSL must have (exactly this way, including :jpa)
	annotationProcessor 'jakarta.persistence:jakarta.persistence-api' // QueryDSL must have (Gradle 5+)
    annotationProcessor 'jakarta.annotation:jakarta.annotation-api' // QueryDSL must have (Gradle 5+)

	developmentOnly 'org.springframework.boot:spring-boot-devtools'

 	compile 'com.github.jsimone:webapp-runner:8.5.11.3' // Heroku WAR deploy

	runtimeOnly 'mysql:mysql-connector-java'
	providedRuntime 'org.springframework.boot:spring-boot-starter-tomcat'
}

test {
	useJUnitPlatform()
}

compileJava { // QueryDSL must have (.\gradlew clean build)
    options.compilerArgs << "-s" // Specific output directory
    options.compilerArgs << "$projectDir/src/main/java" // Output directory
}

task stage() { // Heroku WAR deploy
    dependsOn clean, war
}
war.mustRunAfter clean // Heroku WAR deploy

task copyToLib(type: Copy) { // Heroku WAR deploy
    into "$buildDir/server"
    from(configurations.compile) {
        include "webapp-runner*"
    }
}

stage.dependsOn(copyToLib) // Heroku WAR deploy